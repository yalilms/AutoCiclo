plugins {
  id 'application'
  id 'org.openjfx.javafxplugin' version '0.1.0' //Plugin que ayuda a construir proyectos JAVAFX
}

repositories {
	mavenCentral()
	maven {
		url "https://jaspersoft.jfrog.io/jaspersoft/third-party-ce-artifacts/"
	}
	maven {
		url "https://jaspersoft.jfrog.io/jaspersoft/jr-ce-releases/"
	}
}

javafx {
    version = "25"
    modules = [ 'javafx.base', 'javafx.swing', 'javafx.graphics', 'javafx.controls', 'javafx.fxml', 'javafx.media', 'javafx.web']
}

dependencies {
  // Driver MySQL 
    implementation 'com.mysql:mysql-connector-j:8.2.0'

    // AnimateFX removed due to build issues
    // implementation 'io.github.typhon0:AnimateFX:1.3.0'

    // Ikonli 
    implementation 'org.kordamp.ikonli:ikonli-javafx:12.3.1'
    implementation 'org.kordamp.ikonli:ikonli-materialdesign2-pack:12.3.1'

    // Gson
    implementation 'com.google.code.gson:gson:2.10.1'

    // JasperReports - versión 6.20.6 (última estable LTS)
    implementation 'net.sf.jasperreports:jasperreports:6.20.6'

    // JavaFX WebView (ya incluido en javafx.web pero por si acaso)
    implementation 'org.openjfx:javafx-web:25'
}

java { //Se indica la versión de JDK que ha de utilizar
    toolchain {
        languageVersion = JavaLanguageVersion.of(24)
    }
}

application { //Definimos clase principal del proyecto
    mainClass = 'com.autociclo.Main'
    
    // Agregar JVM args para permitir acceso de javafx.web a clases internas
    applicationDefaultJvmArgs = [
        // JavaFX Graphics exports
        '--add-exports', 'javafx.graphics/com.sun.javafx.sg.prism=ALL-UNNAMED',
        '--add-exports', 'javafx.graphics/com.sun.javafx.sg.prism.web=ALL-UNNAMED',
        '--add-exports', 'javafx.graphics/com.sun.javafx.scene=ALL-UNNAMED',
        '--add-exports', 'javafx.graphics/com.sun.javafx.util=ALL-UNNAMED',
        '--add-exports', 'javafx.graphics/com.sun.javafx.tk=ALL-UNNAMED',
        '--add-exports', 'javafx.graphics/com.sun.prism=ALL-UNNAMED',
        '--add-exports', 'javafx.graphics/com.sun.glass.ui=ALL-UNNAMED',
        '--add-exports', 'javafx.graphics/com.sun.glass.utils=ALL-UNNAMED',
        '--add-exports', 'javafx.graphics/com.sun.javafx.geom=ALL-UNNAMED',
        '--add-exports', 'javafx.graphics/com.sun.javafx.geom.transform=ALL-UNNAMED',
        // JavaFX Base exports
        '--add-exports', 'javafx.base/com.sun.javafx.logging=ALL-UNNAMED',
        // JavaFX Web exports
        '--add-exports', 'javafx.web/com.sun.webkit=ALL-UNNAMED',
        '--add-exports', 'javafx.web/com.sun.javafx.webkit.prism=ALL-UNNAMED',
        '--add-exports', 'javafx.web/com.sun.javafx.webkit.theme=ALL-UNNAMED'
    ]
}

// Tarea personalizada para compilar informes JasperReports
task compilarInformes(type: JavaExec) {
    group = 'build'
    description = 'Compila archivos .jrxml a .jasper'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.autociclo.utils.CompiladorInformes'
    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(24)
    }
}

sourceSets {
  main {
    resources {
        srcDirs = ["src/main/resources"]
        includes = ["**/*.fxml","**/*.png","**/*.properties","**/*.css","**/*.jpg","**/*.jpeg","**/*.json","**/*.jrxml","**/*.jasper"]  //Importante para poder abrir una extensión no reconocida
    }
  }
}





